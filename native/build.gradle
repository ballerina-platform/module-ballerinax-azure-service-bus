/*
 * Copyright (c) 2023, WSO2 LLC. (http://www.wso2.org)
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs'
}

description = 'Ballerina - Azure Service Bus Native'

configurations {
    dist {
        transitive true
    }
}

dependencies {
    checkstyle project(":checkstyle")
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstylePluginVersion}"

    implementation group: 'org.ballerinalang', name: 'ballerina-runtime', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'ballerina-lang', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'value', version: "${ballerinaLangVersion}"
    implementation group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"
    implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: "${azureServiceBusVersion}"

    dist (group: 'com.azure', name: 'azure-messaging-servicebus', version: "${azureServiceBusVersion}") {
        exclude group: "com.fasterxml.woodstox", module: "woodstox-core"
        exclude group: "org.codehaus.woodstox", module: "stax2-api"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "io.netty", module: "netty-buffer"
        exclude group: "io.netty", module: "netty-codec"
        exclude group: "io.netty", module: "netty-codec-dns"
        exclude group: "io.netty", module: "netty-common"
        exclude group: "io.netty", module: "netty-resolver"
        exclude group: "io.netty", module: "netty-transport"
        exclude group: "io.netty", module: "netty-transport-native-unix-common"
        exclude group: "io.netty", module: "netty-handler-proxy"
        exclude group: "io.netty", module: "netty-codec-socks"
        exclude group: "io.netty", module: "netty-handler"
        exclude group: "io.netty", module: "netty-codec-http"
        exclude group: "io.netty", module: "netty-codec-http2"
        exclude group: "io.netty", module: "netty-transport-native-epoll"
        exclude group: "io.netty", module: "netty-transport-classes-epoll"
        exclude group: "io.netty", module: "netty-transport-native-kqueue"
        exclude group: "io.netty", module: "netty-transport-classes-kqueue"
        exclude group: "io.netty", module: "netty-tcnative-boringssl-static"
        exclude group: "io.netty", module: "netty-tcnative-classes"
        exclude group: "io.netty", module: "netty-resolver-dns"
        exclude group: "io.netty", module: "netty-resolver-dns-native-macos"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    testLogging {
        showStackTraces = true
        showStandardStreams = true
        events "failed"
        exceptionFormat "full"
    }
    jacoco {
        enabled = true
        destinationFile = file("$buildDir/coverage-reports/jacoco.exec")
        includeNoLocationClasses = true
    }
}

spotbugsMain {
    def classLoader = plugins["com.github.spotbugs"].class.classLoader
    def SpotBugsConfidence = classLoader.findLoadedClass("com.github.spotbugs.snom.Confidence")
    def SpotBugsEffort = classLoader.findLoadedClass("com.github.spotbugs.snom.Effort")
    effort = SpotBugsEffort.MAX
    reportLevel = SpotBugsConfidence.LOW
    ignoreFailures = true
    reportsDir = file("$project.buildDir/reports/spotbugs")
    def excludeFile = file("${rootDir}/build-config/spotbugs-exclude.xml")
    if (excludeFile.exists()) {
        it.excludeFilter = excludeFile
    }
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugsTest {
    enabled = false
}

tasks.withType(Checkstyle) {
    exclude '**/module-info.java'
}

checkstyle {
    toolVersion "${checkstylePluginVersion}"
    configFile file("${rootDir}/build-config/checkstyle/build/checkstyle.xml")
    configProperties = ["suppressionFile": file("${rootDir}/build-config/checkstyle/build/suppressions.xml")]
}

checkstyleMain.dependsOn ':checkstyle:downloadCheckstyleRuleFiles'

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn configurations.dist
    from { configurations.dist.collect { it.isDirectory() ? it : zipTree(it) } } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA', 'org/slf4j/**'
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}
