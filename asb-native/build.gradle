plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs'
    id "com.github.johnrengelman.shadow"
}

group project.group
version project.version

configurations {
    dist {
        transitive true
    }
}

repositories {
    mavenCentral()

    maven {
        url = 'https://maven.pkg.github.com/ballerina-platform/ballerina-lang'
        credentials {
            username System.getenv("packageUser")
            password System.getenv("packagePAT")
        }
    }
}

dependencies {
    checkstyle project(':checkstyle')
    checkstyle "com.puppycrawl.tools:checkstyle:${puppycrawlCheckstyleVersion}"
    implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: "${azureServiceBusVersion}"
    implementation  'org.slf4j', name: 'slf4j-jdk14', version: "{slf4jVersion}"
    implementation group: 'org.ballerinalang', name: 'ballerina-lang', version: project.ballerinaLangVersion
    implementation(group: 'org.ballerinalang', name: 'ballerina-runtime', version: project.ballerinaLangVersion) { transitive = false }
    implementation group: 'org.ballerinalang', name: 'value', version: "${ballerinaLangVersion}"
    dist group: 'com.azure', name: 'azure-messaging-servicebus', version: "${azureServiceBusVersion}"
}

checkstyle {
    toolVersion "${checkstyleToolVersion}"
    configFile rootProject.file("build-config/checkstyle/build/checkstyle.xml")
    configProperties = ["suppressionFile": file("${rootDir}/build-config/checkstyle/build/suppressions.xml")]
}

def excludePattern = '**/module-info.java'
tasks.withType(Checkstyle).configureEach {
    exclude excludePattern
}

spotbugsMain {
    ignoreFailures = true
    effort "max"
    reportLevel "low"
    reportsDir = file("$project.buildDir/reports/spotbugs")
    def excludeFile = file("${rootDir}/build-config/spotbugs-exclude.xml")
    if (excludeFile.exists()) {
        excludeFilter = excludeFile
    }
    reports {
        html.enabled true
        text.enabled = true
    }
}

spotbugsTest {
    enabled = false
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn configurations.dist
    from { configurations.dist.collect { it.isDirectory() ? it : zipTree(it) } } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA', 'io/netty/**', 'com/google/gson/**',
                'org/slf4j/**', 'org/apache/commons/lang3/**', 'com/ctc/wstx/**', 'org/codehaus/**'
    }
}

checkstyleMain.dependsOn(":checkstyle:downloadCheckstyleRuleFiles")
checkstyleTest.dependsOn(":checkstyle:downloadCheckstyleRuleFiles")
build.dependsOn(shadowJar)
